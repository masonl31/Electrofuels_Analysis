EPrice_RST(IR,S,T)=QEEQ.M(IR,S,T)*MONEYCONVERT/IHOURSINST(S,T);

HPrice_AST(IA,S,T)=QHEQ.M(IA,S,T)*MONEYCONVERT/IHOURSINST(S,T);

WASTEIMPORTED('DENMARK')=SUM((S,ITWWT),VFLOWSOURCE.L('UK_AAA','Imported_RDF','IMPORTEDRDFFLOW',S,ITWWT)*IHOURSINSTWWT(S,ITWWT))*IOF0000001;

WASTE_GATEFEE=QEQFLOWA_SOURCE.M('UK_AAA','Imported_RDF','IMPORTEDRDFFLOW')*MONEYCONVERT;





INCINERATION_CAPACITY(IA,PROC)=VPROCKAPNEW.L(IA,PROC)$APROCKAPNEW(IA,PROC);

INCINERATION_CAPACITY_DK(PROC)=SUM(IA,INCINERATION_CAPACITY(IA,PROC)$APROCKAPNEW(IA,PROC));

INCINERATION_FLOWS(IA,PROC,FLOW,INCIN_TYPEFLOW,S,T)=SUM(IPROCFROM,VFLOW.L(IA,IPROCFROM,PROC,FLOW,S,T)$INCINFLOW(PROC,FLOW,INCIN_TYPEFLOW));
INCINERATION_FLOWS(IA,PROC,'ELECFLOW','ELECTRICITY',S,T)=VFLOW.L(IA,PROC,'ElecBuffer','ELECFLOW',S,T)$SUM(IFLOWIN,INCINFLOW(PROC,IFLOWIN,'MASSFLOW'));
INCINERATION_FLOWS(IA,PROC,'HEATFLOW','HEAT',S,T)=VFLOW.L(IA,PROC,'HeatBuffer','HEATFLOW',S,T)$SUM(IFLOWIN,INCINFLOW(PROC,IFLOWIN,'MASSFLOW'));

INCINERATION_FLOWS_S(IA,PROC,FLOW,INCIN_TYPEFLOW,S)=SUM(T,INCINERATION_FLOWS(IA,PROC,FLOW,INCIN_TYPEFLOW,S,T));

INCINERATION_FLOWS_Y(IA,PROC,FLOW,INCIN_TYPEFLOW)=SUM((S,T),INCINERATION_FLOWS(IA,PROC,FLOW,INCIN_TYPEFLOW,S,T)*IHOURSINST(S,T));

INCINERATION_FLOWS_Y_UNITS(IA,PROC,FLOW,'MASSFLOW')=INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')*IOF0000001;
INCINERATION_FLOWS_Y_UNITS(IA,PROC,FLOW,'ENERGYFLOW')=INCINERATION_FLOWS_Y(IA,PROC,FLOW,'ENERGYFLOW')*IOF0000001;
INCINERATION_FLOWS_Y_UNITS(IA,PROC,FLOW,'ELECTRICITY')=INCINERATION_FLOWS_Y(IA,PROC,FLOW,'ELECTRICITY')*IOF0000001*IOF3P6  ;
INCINERATION_FLOWS_Y_UNITS(IA,PROC,FLOW,'HEAT')=INCINERATION_FLOWS_Y(IA,PROC,FLOW,'HEAT')*IOF0000001*IOF3P6  ;

INCINERATION_FLOWS_C(C,PROC,FLOW,INCIN_TYPEFLOW,S,T)=SUM(IA$ICA(C,IA),INCINERATION_FLOWS(IA,PROC,FLOW,INCIN_TYPEFLOW,S,T));

INCINERATION_FLOWS_S_C(C,PROC,FLOW,INCIN_TYPEFLOW,S)=SUM((IA,T)$ICA(C,IA),INCINERATION_FLOWS(IA,PROC,FLOW,INCIN_TYPEFLOW,S,T));

INCINERATION_FLOWS_Y_C(C,PROC,FLOW,INCIN_TYPEFLOW)=SUM((IA,S,T)$ICA(C,IA),INCINERATION_FLOWS(IA,PROC,FLOW,INCIN_TYPEFLOW,S,T)*IHOURSINST(S,T));

INCINERATION_FLOWS_Y_UNITS_C(C,PROC,FLOW,'MASSFLOW')=INCINERATION_FLOWS_Y_C(C,PROC,FLOW,'MASSFLOW')*IOF0000001;
INCINERATION_FLOWS_Y_UNITS_C(C,PROC,FLOW,'ENERGYFLOW')=INCINERATION_FLOWS_Y_C(C,PROC,FLOW,'ENERGYFLOW')*IOF0000001;
INCINERATION_FLOWS_Y_UNITS_C(C,PROC,FLOW,'ELECTRICITY')=INCINERATION_FLOWS_Y_C(C,PROC,FLOW,'ELECTRICITY')*IOF0000001*IOF3P6  ;
INCINERATION_FLOWS_Y_UNITS_C(C,PROC,FLOW,'HEAT')=INCINERATION_FLOWS_Y_C(C,PROC,FLOW,'HEAT')*IOF0000001*IOF3P6  ;

*_____________________________________
INCINERATION_FLOWS_TOTAL(C,INCIN_TYPEFLOW,S,T)=SUM((IA,PROC,FLOW)$ICA(C,IA),INCINERATION_FLOWS(IA,PROC,FLOW,INCIN_TYPEFLOW,S,T));

INCINERATION_FLOWS_S_TOTAL(C,INCIN_TYPEFLOW,S)=SUM((IA,PROC,FLOW,T)$ICA(C,IA),INCINERATION_FLOWS(IA,PROC,FLOW,INCIN_TYPEFLOW,S,T));

INCINERATION_FLOWS_Y_TOTAL(C,INCIN_TYPEFLOW)=SUM((IA,PROC,FLOW,S,T)$ICA(C,IA),INCINERATION_FLOWS(IA,PROC,FLOW,INCIN_TYPEFLOW,S,T)*IHOURSINST(S,T));

INCINERATION_FLOWS_Y_UNITS_TOTAL(C,'MASSFLOW')=INCINERATION_FLOWS_Y_TOTAL(C,'MASSFLOW')*IOF0000001;
INCINERATION_FLOWS_Y_UNITS_TOTAL(C,'ENERGYFLOW')=INCINERATION_FLOWS_Y_TOTAL(C,'ENERGYFLOW')*IOF0000001;
INCINERATION_FLOWS_Y_UNITS_TOTAL(C,'ELECTRICITY')=INCINERATION_FLOWS_Y_TOTAL(C,'ELECTRICITY')*IOF0000001*IOF3P6  ;
INCINERATION_FLOWS_Y_UNITS_TOTAL(C,'HEAT')=INCINERATION_FLOWS_Y_TOTAL(C,'HEAT')*IOF0000001*IOF3P6  ;

WASTETRANSPORT=SUM((IAE,IAI,IFLOWEXIM,S,ITWWT),VFLOWTRANS.L(IAE,IAI,'ProcEximTruck',IFLOWEXIM,S,ITWWT)*TRANSDIST('ProcEximTruck',IAE,IAI)*IHOURSINSTWWT(S,ITWWT));

WASTE_TDISTANCE=(SUM((IAE,IAI,S,ITWWT),VFLOWTRANS.L(IAE,IAI,'ProcEximTruck','RESIDUALWASTEHH',S,ITWWT)*TRANSDIST('ProcEximTruck',IAE,IAI)*IHOURSINSTWWT(S,ITWWT))+
                 SUM((IAE,IAI,S,ITWWT),VFLOWTRANS.L(IAE,IAI,'ProcEximTruck','INDUSTRY_MIXEDWASTE',S,ITWWT)*TRANSDIST('ProcEximTruck',IAE,IAI)*IHOURSINSTWWT(S,ITWWT)))/
                (SUM((IA,S,ITWWT),VFLOWSOURCE.L(IA,'HouseHoldWasteGen','RESIDUALWASTEHH',S,ITWWT)*IHOURSINSTWWT(S,ITWWT))+
                 SUM((IA,S,ITWWT),VFLOWSOURCE.L(IA,'IndustrialWasteGen','INDUSTRY_MIXEDWASTE',S,ITWWT)*IHOURSINSTWWT(S,ITWWT)));

WASTE_TDISTANCEALL=(SUM((IAE,IAI,IFLOWEXIM,S,ITWWT),VFLOWTRANS.L(IAE,IAI,'ProcEximTruck',IFLOWEXIM,S,ITWWT)*TRANSDIST('ProcEximTruck',IAE,IAI)*IHOURSINSTWWT(S,ITWWT)))
                   /(SUM((IA,S,ITWWT),VFLOWSOURCE.L(IA,'HouseHoldWasteGen','RESIDUALWASTEHH',S,ITWWT)*IHOURSINSTWWT(S,ITWWT))+
                     SUM((IA,S,ITWWT),VFLOWSOURCE.L(IA,'IndustrialWasteGen','INDUSTRY_MIXEDWASTE',S,ITWWT)*IHOURSINSTWWT(S,ITWWT))+
                     SUM((S,ITWWT),VFLOWSOURCE.L('UK_AAA','Imported_RDF','IMPORTEDRDFFLOW',S,ITWWT)*IHOURSINSTWWT(S,ITWWT)));



LCOT_A(IA,PROC,'Expenses')$((IAPROCKAPNEW(IA,PROC) OR SUM((FLOW,IFLOWINOUT),IPROCKAPFX(IA,PROC,FLOW,IFLOWINOUT))) AND (SUM(FLOW,INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1))=
                          ((IOF1000000*PROCDATA(PROC,'PROCINVCOST')*VPROCKAPNEW.L(IA,PROC)*(RateOfReturn/(1-((1+RateOfReturn)**(-PROCDATA(PROC,'PROCLIFETIME')))))
                          +SUM((FLOW,IFLOWINOUT)$PROCKAPDATA(PROC,FLOW,IFLOWINOUT),PROCDATA(PROC,'PROCFIXCOST')*(VPROCKAPNEW.L(IA,PROC)$(IAPROCKAPNEW(IA,PROC))+IPROCKAPFX(IA,PROC,FLOW,IFLOWINOUT))))$IAPROCKAPNEW(IA,PROC)
                          +SUM((S,T),(VFLOW.L(IA,'Money_buffer_T',PROC,'OPERATIONCOST',S,T)*IHOURSINST(S,T))$(SUM(TECHTYPE,MSWTECHTYPE(PROC,TECHTYPE))))
                          +SUM(FLOW,INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW'))*WASTE_TDISTANCEALL*TRANSCOST('TCDMONEYKMTON','ITRANSDWDISTWEIGHT','ProcEximTruck','OPERATIONCOST')
                          )
                          /SUM(FLOW,INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW'))
                          ;

LCOT_A(IA,PROC,'Earnings_Elec')$((IAPROCKAPNEW(IA,PROC) OR SUM((FLOW,IFLOWINOUT),IPROCKAPFX(IA,PROC,FLOW,IFLOWINOUT))) AND (SUM(FLOW,INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1))=
                          (SUM((S,T),(INCINERATION_FLOWS(IA,PROC,'ELECFLOW','ELECTRICITY',S,T)*SUM(IR$RRRAAA(IR,IA),EPrice_RST(IR,S,T))
                          )*IHOURSINST(S,T)))
                          /SUM(FLOW,INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW'))
                          ;

LCOT_A(IA,PROC,'Earnings_Heat')$((IAPROCKAPNEW(IA,PROC) OR SUM((FLOW,IFLOWINOUT),IPROCKAPFX(IA,PROC,FLOW,IFLOWINOUT))) AND (SUM(FLOW,INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1))=
                          (SUM((S,T),(INCINERATION_FLOWS(IA,PROC,'HEATFLOW','HEAT',S,T)*HPrice_AST(IA,S,T))*IHOURSINST(S,T)))
                          /SUM(FLOW,INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW'))
                          ;


LCOT_A(IA,PROC,'Balance')$((IAPROCKAPNEW(IA,PROC) OR SUM((FLOW,IFLOWINOUT),IPROCKAPFX(IA,PROC,FLOW,IFLOWINOUT))) AND (SUM(FLOW,INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1))=
                         LCOT_A(IA,PROC,'Earnings_Elec')+LCOT_A(IA,PROC,'Earnings_Heat')-LCOT_A(IA,PROC,'Expenses');



LCOT_DK(PROC,'Expenses')$(SUM((IA,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1)=SUM(IA,
                          ((IOF1000000*PROCDATA(PROC,'PROCINVCOST')*VPROCKAPNEW.L(IA,PROC)*(RateOfReturn/(1-((1+RateOfReturn)**(-PROCDATA(PROC,'PROCLIFETIME')))))
                          +SUM((FLOW,IFLOWINOUT)$PROCKAPDATA(PROC,FLOW,IFLOWINOUT),PROCDATA(PROC,'PROCFIXCOST')*(VPROCKAPNEW.L(IA,PROC)$(IAPROCKAPNEW(IA,PROC))+IPROCKAPFX(IA,PROC,FLOW,IFLOWINOUT))))$IAPROCKAPNEW(IA,PROC)
                          +SUM((S,T),(VFLOW.L(IA,'Money_buffer_T',PROC,'OPERATIONCOST',S,T)*IHOURSINST(S,T))$(SUM(TECHTYPE,MSWTECHTYPE(PROC,TECHTYPE))))
                          +SUM(FLOW,INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW'))*WASTE_TDISTANCEALL*TRANSCOST('TCDMONEYKMTON','ITRANSDWDISTWEIGHT','ProcEximTruck','OPERATIONCOST'))
                          )
                          /SUM((IA,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW'))
                          ;

LCOT_DK(PROC,'Earnings')$(SUM((IA,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1)= SUM(IA,
                          (SUM((S,T),(INCINERATION_FLOWS(IA,PROC,'ELECFLOW','ELECTRICITY',S,T)*SUM(IR$RRRAAA(IR,IA),EPrice_RST(IR,S,T))
                          +INCINERATION_FLOWS(IA,PROC,'HEATFLOW','HEAT',S,T)*HPrice_AST(IA,S,T))*IHOURSINST(S,T))))
                          /SUM((IA,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW'))
                          ;

LCOT_DK(PROC,'Balance')$(SUM((IA,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1)=
                         LCOT_DK(PROC,'Earnings')-LCOT_DK(PROC,'Expenses');



LCOT_DK_AV('Expenses')=SUM((IA,PROC)$(SUM(FLOW,(INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW') GT 0.1))),
                          ((IOF1000000*PROCDATA(PROC,'PROCINVCOST')*VPROCKAPNEW.L(IA,PROC)*(RateOfReturn/(1-((1+RateOfReturn)**(-PROCDATA(PROC,'PROCLIFETIME')))))
                          +SUM((FLOW,IFLOWINOUT)$PROCKAPDATA(PROC,FLOW,IFLOWINOUT),PROCDATA(PROC,'PROCFIXCOST')*(VPROCKAPNEW.L(IA,PROC)$(IAPROCKAPNEW(IA,PROC))+IPROCKAPFX(IA,PROC,FLOW,IFLOWINOUT))))$IAPROCKAPNEW(IA,PROC)
                          +SUM((S,T),(VFLOW.L(IA,'Money_buffer_T',PROC,'OPERATIONCOST',S,T)*IHOURSINST(S,T))$(SUM(TECHTYPE,MSWTECHTYPE(PROC,TECHTYPE))))
                          +SUM(FLOW,INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW'))*WASTE_TDISTANCEALL*TRANSCOST('TCDMONEYKMTON','ITRANSDWDISTWEIGHT','ProcEximTruck','OPERATIONCOST'))
                          )
                          /SUM((IA,PROC,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')$(INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW') GT 0.1))
                          ;

LCOT_DK_AV('Earnings')$(SUM((IA,PROC,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1)= SUM((IA,PROC)$(SUM(FLOW,(INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW') GT 0.1))),
                          (SUM((S,T),(INCINERATION_FLOWS(IA,PROC,'ELECFLOW','ELECTRICITY',S,T)*SUM(IR$RRRAAA(IR,IA),EPrice_RST(IR,S,T))
                          +INCINERATION_FLOWS(IA,PROC,'HEATFLOW','HEAT',S,T)*HPrice_AST(IA,S,T))*IHOURSINST(S,T))))
                          /SUM((IA,PROC,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')$(INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW') GT 0.1))
                          ;

LCOT_DK_AV('Balance')$(SUM((IA,PROC,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1)=LCOT_DK_AV('Earnings')-LCOT_DK_AV('Expenses');


LCOT_DK_TOTAL('Expenses')$(SUM((IA,PROC,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1)=SUM((IA,PROC)$(SUM(FLOW,(INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW') GT 0.1))),
                          ((IOF1000000*PROCDATA(PROC,'PROCINVCOST')*VPROCKAPNEW.L(IA,PROC)*(RateOfReturn/(1-((1+RateOfReturn)**(-PROCDATA(PROC,'PROCLIFETIME')))))
                          +SUM((FLOW,IFLOWINOUT)$PROCKAPDATA(PROC,FLOW,IFLOWINOUT),PROCDATA(PROC,'PROCFIXCOST')*(VPROCKAPNEW.L(IA,PROC)$(IAPROCKAPNEW(IA,PROC))+IPROCKAPFX(IA,PROC,FLOW,IFLOWINOUT))))$IAPROCKAPNEW(IA,PROC)
                          +SUM((S,T),(VFLOW.L(IA,'Money_buffer_T',PROC,'OPERATIONCOST',S,T)*IHOURSINST(S,T))$(SUM(TECHTYPE,MSWTECHTYPE(PROC,TECHTYPE))))
                          +SUM(FLOW,INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW'))*WASTE_TDISTANCEALL*TRANSCOST('TCDMONEYKMTON','ITRANSDWDISTWEIGHT','ProcEximTruck','OPERATIONCOST'))
                          )*IOF0000001
                          ;

LCOT_DK_TOTAL('Earnings')$(SUM((IA,PROC,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1)= SUM((IA,PROC)$(SUM(FLOW,(INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW') GT 0.1))),
                          (SUM((S,T),(INCINERATION_FLOWS(IA,PROC,'ELECFLOW','ELECTRICITY',S,T)*SUM(IR$RRRAAA(IR,IA),EPrice_RST(IR,S,T))
                          +INCINERATION_FLOWS(IA,PROC,'HEATFLOW','HEAT',S,T)*HPrice_AST(IA,S,T))*IHOURSINST(S,T))))
                          *IOF0000001
                          ;

LCOT_DK_TOTAL('Earnings_Elec')$(SUM((IA,PROC,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1)= SUM((IA,PROC)$(SUM(FLOW,(INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW') GT 0.1))),
                          (SUM((S,T),(INCINERATION_FLOWS(IA,PROC,'ELECFLOW','ELECTRICITY',S,T)*SUM(IR$RRRAAA(IR,IA),EPrice_RST(IR,S,T))
                          )*IHOURSINST(S,T))))
                          *IOF0000001
                          ;

LCOT_DK_TOTAL('Earnings_Heat')$(SUM((IA,PROC,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1)= SUM((IA,PROC)$(SUM(FLOW,(INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW') GT 0.1))),
                          (SUM((S,T),(INCINERATION_FLOWS(IA,PROC,'HEATFLOW','HEAT',S,T)*HPrice_AST(IA,S,T))*IHOURSINST(S,T))))
                          *IOF0000001
                          ;

LCOT_DK_TOTAL('Balance')$(SUM((IA,PROC,FLOW),INCINERATION_FLOWS_Y(IA,PROC,FLOW,'MASSFLOW')) GT 0.1)=LCOT_DK_TOTAL('Earnings')-LCOT_DK_TOTAL('Expenses');



* MODELING OF THE ELECTRICITY DEMAND


EDemand_RST("classic",IR,S,T)$(NOT (RETRANS(IR) OR RINDUS(IR)))=IDE_T_Y(IR,S,T);

EDemand_RST("transport",IR,S,T)=SUM(RETRANS$IXKINI_Y(IR,RETRANS),IDE_T_Y(RETRANS,S,T));

EDemand_RST("individual heating",IR,S,T)=SUM(IAGK_Y(IA,IGE)$((RRRAAA(IR,IA) AND (AINDHEAT(IA))) AND IGETOH(IGE)), VGE_T.L(IA,IGE,S,T))
$ifi %bb2%==yes                         +SUM(IAGKN(IA,IGE)$((RRRAAA(IR,IA) AND (AINDHEAT(IA))) AND IGETOH(IGE)), VGEN_T.L(IA,IGE,S,T));

EDemand_RST("process heating",IR,S,T)=SUM(RINDUS$IXKINI_Y(IR,RINDUS),IDE_T_Y(RINDUS,S,T));

EDemand_RST("DH",IR,S,T)=SUM(IAGK_Y(IA,IGE)$((RRRAAA(IR,IA) AND (NOT AINDHEAT(IA))) AND IGETOH(IGE)), VGE_T.L(IA,IGE,S,T))
$ifi %bb2%==yes         +SUM(IAGKN(IA,IGE)$((RRRAAA(IR,IA) AND (NOT AINDHEAT(IA))) AND IGETOH(IGE)), VGEN_T.L(IA,IGE,S,T));

$ifi %H2%==yes  EDemand_RST("H2",IR,S,T)=SUM(IAGK_Y(IA,IGETOH2)$(RRRAAA(IR,IA)), VGE_T.L(IA,IGETOH2,S,T))
$ifi %bb2%==yes                         +SUM(IAGKN(IA,IGETOH2)$(RRRAAA(IR,IA)), VGEN_T.L(IA,IGETOH2,S,T));


Edemand_CST(EDemand_type,C,S,T)=SUM(IR$CCCRRR(C,IR),EDemand_RST(EDemand_type,IR,S,T));


Edemand_R(EDemand_type,IR)=SUM((S,T),EDemand_RST(EDemand_type,IR,S,T)*IHOURSINST(S,T))*IOF0000001*IOF3P6;


Edemand_C(EDemand_type,C)=SUM((S,T),EDemand_CST(EDemand_type,C,S,T)*IHOURSINST(S,T))*IOF0000001*IOF3P6;

Hdemand_AST(IA,S,T)$(NOT AINDHEAT(IA))=IDH_T_Y(IA,S,T);


*Weighted average electricity price by consumption quantity (€ 2015/MWh)

EPrice_R(IR)$(SUM((EDemand_type,S,T),EDemand_RST(EDemand_type,IR,S,T)*IHOURSINST(S,T)))=SUM((EDemand_type,S,T),EDemand_RST(EDemand_type,IR,S,T)*IHOURSINST(S,T)*EPrice_RST(IR,S,T))/SUM((EDemand_type,S,T),EDemand_RST(EDemand_type,IR,S,T)*IHOURSINST(S,T));

EPrice_CST(C,S,T)=SUM((EDemand_type,IR)$CCCRRR(C,IR),EDemand_RST(EDemand_type,IR,S,T)*EPrice_RST(IR,S,T))/SUM((EDemand_type,IR)$CCCRRR(C,IR),EDemand_RST(EDemand_type,IR,S,T));

EPrice_C(C)=SUM((EDemand_type,S,T),EDemand_CST(EDemand_type,C,S,T)*IHOURSINST(S,T)*EPrice_CST(C,S,T))/SUM((EDemand_type,S,T),EDemand_CST(EDemand_type,C,S,T)*IHOURSINST(S,T));

HPrice_AS(IA,S)$(SUM(T,Hdemand_AST(IA,S,T)))=SUM(T,HPrice_AST(IA,S,T)*Hdemand_AST(IA,S,T)*IHOURSINST(S,T))/SUM(T,Hdemand_AST(IA,S,T)*IHOURSINST(S,T));

HPrice_A(IA)$(SUM((S,T),Hdemand_AST(IA,S,T)))=SUM((S,T),HPrice_AST(IA,S,T)*Hdemand_AST(IA,S,T)*IHOURSINST(S,T))/SUM((S,T),Hdemand_AST(IA,S,T)*IHOURSINST(S,T));

HPrice_C(CCC)$(SUM((S,T,IA)$ICA(CCC,IA),Hdemand_AST(IA,S,T)))=SUM((S,T,IA)$ICA(CCC,IA),HPrice_AST(IA,S,T)*Hdemand_AST(IA,S,T)*IHOURSINST(S,T))/SUM((S,T,IA)$ICA(CCC,IA),Hdemand_AST(IA,S,T)*IHOURSINST(S,T));


EProduction_RST(IR,TECHTYPE,S,T)= SUM(IAGK_Y(IA,IGE)$((RRRAAA(IR,IA) AND (NOT AAAOFFSHORE(IA))) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)), VGE_T.L(IA,IGE,S,T))
$ifi %bb2%==yes                  +SUM(IAGKN(IA,IGE)$((RRRAAA(IR,IA) AND (NOT AAAOFFSHORE(IA))) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)), VGEN_T.L(IA,IGE,S,T))
                                 +SUM(RRROFFSHORE$IXKINI_Y(RRROFFSHORE,IR),(SUM(IAGK_Y(IA,IGE)$((RRRAAA(RRROFFSHORE,IA) AND (AAAOFFSHORE(IA))) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)), VGE_T.L(IA,IGE,S,T)))*(1-XLOSS(RRROFFSHORE,IR)))
$ifi %bb2%==yes                  +SUM(RRROFFSHORE$IXKINI_Y(RRROFFSHORE,IR),(SUM(IAGKN(IA,IGE)$((RRRAAA(RRROFFSHORE,IA) AND (AAAOFFSHORE(IA))) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)), VGEN_T.L(IA,IGE,S,T)))*(1-XLOSS(RRROFFSHORE,IR)))

$if     EXIST '../addons/_hooks/EProduction_RST.inc' $INCLUDE      '../addons/_hooks/EProduction_RST.inc';
$if not EXIST '../addons/_hooks/EProduction_RST.inc' $INCLUDE '../../base/addons/_hooks/EProduction_RST.inc';

;

ETrade_RST(IR,"Import",S,T)$((NOT RETRANS(IR)) AND (NOT RINDUS(IR)))=SUM(IRE$((IXKINI_Y(IRE,IR) OR IXKN(IRE,IR) OR IXKN(IR,IRE)) AND (NOT RRROFFSHORE(IRE))), VX_T.L(IRE,IR,S,T)*(1-XLOSS(IRE,IR)));
ETrade_RST(IR,"Export",S,T)$(NOT RRROFFSHORE(IR))= SUM(IRI$((IXKINI_Y(IR,IRI) OR IXKN(IR,IRI) OR IXKN(IRI,IR)) AND ((NOT RETRANS(IRI)) AND (NOT RINDUS(IRI)))),VX_T.L(IR,IRI,S,T));
ETrade_RST(IR,"Net Export",S,T)=ETrade_RST(IR,"Export",S,T)-ETrade_RST(IR,"Import",S,T);

Eproduction_CST(C,TECHTYPE,S,T)=SUM(IR$CCCRRR(C,IR),EProduction_RST(IR,TECHTYPE,S,T));

ETrade_CST(C,"Import",S,T)=SUM((IR,IRE)$(CCCRRR(C,IR) AND (NOT CCCRRR(C,IRE))AND (IXKINI_Y(IRE,IR) OR IXKN(IR,IRE) OR IXKN(IRE,IR)) AND (IXKINI_Y(IRE,IR) NE INF)), VX_T.L(IRE,IR,S,T)*(1-XLOSS(IRE,IR)));
ETrade_CST(C,"Export",S,T)=SUM((IR,IRI)$(CCCRRR(C,IR) AND (NOT CCCRRR(C,IRI))AND (IXKINI_Y(IR,IRI) OR IXKN(IRI,IR) OR IXKN(IR,IRI)) AND (IXKINI_Y(IR,IRI) NE INF)), VX_T.L(IR,IRI,S,T));
ETrade_CST(C,"Net Export",S,T)=ETrade_CST(C,"Export",S,T)-ETrade_CST(C,"Import",S,T);


EProduction_R(IR,TECHTYPE)=(SUM((S,T),EProduction_RST(IR,TECHTYPE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

EProduction_C(C,TECHTYPE)= (SUM((S,T),EProduction_CST(C,TECHTYPE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

ETrade_R(IR,ELECTRADE)=(SUM((S,T),ETrade_RST(IR,ELECTRADE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

ETrade_C(C,ELECTRADE)=(SUM((S,T),ETrade_CST(C,ELECTRADE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

EProduction_RST(IR,"Net Trade",S,T)=-ETrade_RST(IR,"Net Export",S,T);
Eproduction_CST(C,"Net Trade",S,T)=-ETrade_CST(C,"Net Export",S,T);


*HEAT GENERATION


Hproduction_AST(IA,TECHTYPE,S,T)$(NOT AINDHEAT(IA))=
                                  SUM(IGBPR$(IAGK_Y(IA,IGBPR) AND GTECHTYPE(IGBPR,TECHTYPE)),VGH_T.L(IA,IGBPR,S,T))
$ifi %bb2%==yes                 + SUM(IGBPR$(IAGKN(IA,IGBPR) AND GTECHTYPE(IGBPR,TECHTYPE)),VGHN_T.L(IA,IGBPR,S,T))
                                + SUM(IGEXT$(IAGK_Y(IA,IGEXT) AND GTECHTYPE(IGEXT,TECHTYPE)),VGH_T.L(IA,IGEXT,S,T))
$ifi %bb2%==yes                 + SUM(IGEXT$(IAGKN(IA,IGEXT) AND GTECHTYPE(IGEXT,TECHTYPE)),VGHN_T.L(IA,IGEXT,S,T))
                                + SUM(IGHH$(IAGK_Y(IA,IGHH) AND GTECHTYPE(IGHH,TECHTYPE)),VGH_T.L(IA,IGHH,S,T))
$ifi %bb2%==yes                 + SUM(IGHH$(IAGKN(IA,IGHH) AND GTECHTYPE(IGHH,TECHTYPE)),VGHN_T.L(IA,IGHH,S,T))
                                + SUM(IGETOH$(IAGK_Y(IA,IGETOH) AND GTECHTYPE(IGETOH,TECHTYPE)),VGH_T.L(IA,IGETOH,S,T))
$ifi %bb2%==yes                 + SUM(IGETOH$(IAGKN(IA,IGETOH) AND GTECHTYPE(IGETOH,TECHTYPE)),VGHN_T.L(IA,IGETOH,S,T))
$ifi %H2%==yes                  +SUM(IGETOH2$(IAGK_Y(IA,IGETOH2) AND GTECHTYPE(IGETOH2,TECHTYPE)), VGH_T.L(IA,IGETOH2,S,T))
$ifi %H2%==yes                  +SUM(IGETOH2$(IAGKN(IA,IGETOH2) AND GTECHTYPE(IGETOH2,TECHTYPE)), VGHN_T.L(IA,IGETOH2,S,T))

$if     EXIST '../addons/_hooks/Hproduction_AST.inc' $INCLUDE      '../addons/_hooks/Hproduction_AST.inc';
$if not EXIST '../addons/_hooks/Hproduction_AST.inc' $INCLUDE '../../base/addons/_hooks/Hproduction_AST.inc';
;

Hproduction_AST(IA,"Heat to Storage",S,T)=VHSTOLOADT.L(IA,S,T)$SUM(IGHSTO$(IAGK_Y(IA,IGHSTO) or IAGKN(IA,IGHSTO)),1);
Hproduction_AST(IA,"Heat cool down",S,T)=VCOOLDOWN.L(IA,S,T);

Hproduction_AS(IA,TECHTYPE,S)=SUM(T,Hproduction_AST(IA,TECHTYPE,S,T)*WEIGHT_T(T)*SUM(TTT,WEIGHT_T(TTT))/IWEIGHSUMT);

Hproduction_A(IA,TECHTYPE)=(SUM((S,T),Hproduction_AST(IA,TECHTYPE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

Hproduction_CST(C,TECHTYPE,S,T)=SUM(IA$ICA(C,IA),Hproduction_AST(IA,TECHTYPE,S,T));

Hproduction_CS(C,TECHTYPE,S)=SUM(IA$ICA(C,IA),Hproduction_AS(IA,TECHTYPE,S));

Hproduction_C(C,TECHTYPE)=SUM(IA$ICA(C,IA),Hproduction_A(IA,TECHTYPE));

FuelConsumption_C(C,TECHTYPE)=SUM((IA,G,S,T)$(ICA(C,IA) AND GTECHTYPE(G,TECHTYPE)),IHOURSINST(S,T)*(VGF_T.L(IA,G,S,T)+VGFN_T.L(IA,G,S,T)))*IOF0000001*IOF3P6
$if     EXIST '../addons/_hooks/FuelConsumption_C.inc' $INCLUDE      '../addons/_hooks/FuelConsumption_C.inc';
$if not EXIST '../addons/_hooks/FuelConsumption_C.inc' $INCLUDE '../../base/addons/_hooks/FuelConsumption_C.inc';

;

FuelConsumption_TOTAL(TECHTYPE)=SUM(C,FuelConsumption_C(C,TECHTYPE));

*_______________________________________________________________________________________________________________________________________________________________________________________________________________________________________


Elec_Capacity(C,'New',TECHTYPE)=SUM((IA,IGE)$(ICA(C,IA) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGE))
),VGKN.L(IA,IGE));

Elec_Capacity(C,'Existing',TECHTYPE)=SUM((IA,IGE)$(ICA(C,IA) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE) AND IAGK_Y(IA,IGE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGE))
),IGKFX_Y(IA,IGE));

Elec_Capacity(C,'TOTAL',TECHTYPE)=Elec_Capacity(C,'New',TECHTYPE)+Elec_Capacity(C,'Existing',TECHTYPE);


Heat_Capacity(C,'New',TECHTYPE)=SUM(IA$(ICA(C,IA) AND (NOT AINDHEAT(IA))),
SUM(IGBPR$(GTECHTYPE(IGBPR,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGBPR))
),VGKN.L(IA,IGBPR)/GDATA(IGBPR,'GDCB'))
+SUM(IGEXT$(GTECHTYPE(IGEXT,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGEXT))
),VGKN.L(IA,IGEXT)/GDATA(IGEXT,'GDCB'))
+SUM(IGHHNOSTO$(GTECHTYPE(IGHHNOSTO,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGHHNOSTO))
),VGKN.L(IA,IGHHNOSTO ))
+SUM(IGETOH$(GTECHTYPE(IGETOH,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGETOH))
),VGKN.L(IA,IGETOH)));

Heat_Capacity(C,'Existing',TECHTYPE)=SUM(IA$(ICA(C,IA) AND (NOT AINDHEAT(IA))),
SUM(IGBPR$(GTECHTYPE(IGBPR,TECHTYPE) AND IAGK_Y(IA,IGBPR)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGBPR))
),IGKFX_Y(IA,IGBPR)/GDATA(IGBPR,'GDCB'))
+SUM(IGEXT$(GTECHTYPE(IGEXT,TECHTYPE) AND IAGK_Y(IA,IGEXT)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGEXT))
),IGKFX_Y(IA,IGEXT)/GDATA(IGEXT,'GDCB'))
+SUM(IGHHNOSTO$(GTECHTYPE(IGHHNOSTO,TECHTYPE) AND IAGK_Y(IA,IGHHNOSTO)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGHHNOSTO))
),IGKFX_Y(IA,IGHHNOSTO ))
+SUM(IGETOH$(GTECHTYPE(IGETOH,TECHTYPE) AND IAGK_Y(IA,IGETOH)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGETOH))
),IGKFX_Y(IA,IGETOH)));

Heat_Capacity(C,'TOTAL',TECHTYPE)=Heat_Capacity(C,'New',TECHTYPE)+Heat_Capacity(C,'Existing',TECHTYPE);





;


*End of the output file
